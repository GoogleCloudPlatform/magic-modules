# Internal HTTP load balancer with a managed instance group backend
# [START cloudloadbalancing_int_http_gce]
# VPC network
resource "google_compute_network" "ilb_network" {
  name                    = "{{index $.Vars "ilb_network_name"}}"
  auto_create_subnetworks = false
}

# proxy-only subnet
resource "google_compute_subnetwork" "proxy_subnet" {
  name          = "{{index $.Vars "proxy_subnet_name"}}"
  ip_cidr_range = "10.0.0.0/24"
  region        = "us-west1"
  purpose       = "REGIONAL_MANAGED_PROXY"
  role          = "ACTIVE"
  network       = google_compute_network.ilb_network.id
}

# backend subnet
resource "google_compute_subnetwork" "ilb_subnet" {
  name          = "{{index $.Vars "backend_subnet_name"}}"
  ip_cidr_range = "10.0.1.0/24"
  region        = "us-west1"
  network       = google_compute_network.ilb_network.id

  depends_on = [
    google_compute_subnetwork.proxy_subnet
  ]
}

# forwarding rule
resource "google_compute_forwarding_rule" "default" {
  name                  = "{{index $.Vars "forwarding_rule_name"}}"
  region                = "us-west1"
  ip_protocol           = "TCP"
  load_balancing_scheme = "INTERNAL_MANAGED"
  port_range            = "80"
  target                = google_compute_region_target_http_proxy.default.id
  network               = google_compute_network.ilb_network.id
  subnetwork            = google_compute_subnetwork.ilb_subnet.id
  network_tier          = "PREMIUM"

  depends_on = [
  	google_compute_subnetwork.proxy_subnet
  ]
}

# HTTP target proxy
resource "google_compute_region_target_http_proxy" "default" {
  name     = "{{index $.Vars "target_http_proxy_name"}}"
  region   = "us-west1"
  url_map  = google_compute_region_url_map.default.id
}

# URL map
resource "google_compute_region_url_map" "default" {
  name            = "tf-test-l7-ilb-regional-url-map%{random_suffix}"
  region          = "us-west1"
  default_service = google_compute_region_backend_service.default.id

  host_rule {
    hosts        = ["service-extensions.com"]
    path_matcher = "callouts"
  }

  path_matcher {
    name            = "callouts"
    default_service = google_compute_region_backend_service.callouts_backend.id
  }
}

# backend service
resource "google_compute_region_backend_service" "default" {
  name                  = "{{index $.Vars "backend_service_name"}}"
  region                = "us-west1"
  protocol              = "HTTP"
  load_balancing_scheme = "INTERNAL_MANAGED"
  timeout_sec           = 10
  health_checks         = [google_compute_region_health_check.default.id]

  backend {
    group           = google_compute_region_instance_group_manager.mig.instance_group
    balancing_mode  = "UTILIZATION"
    capacity_scaler = 1.0
  }
}

# instance template
resource "google_compute_instance_template" "instance_template" {
  name         = "{{index $.Vars "mig_template_name"}}"
  machine_type = "e2-small"
  tags         = ["http-server"]

  network_interface {
    network    = google_compute_network.ilb_network.id
    subnetwork = google_compute_subnetwork.ilb_subnet.id

    access_config {
      # add external ip to fetch packages
    }
  }

  disk {
    source_image = "debian-cloud/debian-10"
    auto_delete  = true
    boot         = true
  }

  # install nginx and serve a simple web page
  metadata = {
    startup-script = <<-EOF1
      #! /bin/bash
      set -euo pipefail

      export DEBIAN_FRONTEND=noninteractive
      apt-get update
      apt-get install -y nginx-light jq

      NAME=$(curl -H "Metadata-Flavor: Google" "http://metadata.google.internal/computeMetadata/v1/instance/hostname")
      IP=$(curl -H "Metadata-Flavor: Google" "http://metadata.google.internal/computeMetadata/v1/instance/network-interfaces/0/ip")
      METADATA=$(curl -f -H "Metadata-Flavor: Google" "http://metadata.google.internal/computeMetadata/v1/instance/attributes/?recursive=True" | jq 'del(.["startup-script"])')

      cat <<EOF > /var/www/html/index.html
      <pre>
      Name: $NAME
      IP: $IP
      Metadata: $METADATA
      </pre>
      EOF
    EOF1
  }

  lifecycle {
    create_before_destroy = true
  }
}

# health check
resource "google_compute_region_health_check" "default" {
  name     = "{{index $.Vars "hc_name"}}"
  region   = "us-west1"

  http_health_check {
    port_specification = "USE_SERVING_PORT"
  }
}

# MIG
resource "google_compute_region_instance_group_manager" "mig" {
  name     = "{{index $.Vars "mig_name"}}"
  region   = "us-west1"

  base_instance_name = "vm"
  target_size        = 2

  version {
    instance_template = google_compute_instance_template.instance_template.id
    name              = "primary"
  }
}

# allow all access from IAP and health check ranges
resource "google_compute_firewall" "fw_iap" {
  name          = "{{index $.Vars "fw_allow_iap_hc_name"}}"
  direction     = "INGRESS"
  network       = google_compute_network.ilb_network.id
  source_ranges = ["130.211.0.0/22", "35.191.0.0/16", "35.235.240.0/20"]

  allow {
    protocol = "tcp"
  }
}

# allow http from proxy subnet to backends
resource "google_compute_firewall" "fw_ilb_to_backends" {
  name          = "{{index $.Vars "fw_allow_ilb_to_backends_name"}}"
  direction     = "INGRESS"
  network       = google_compute_network.ilb_network.id
  source_ranges = ["10.0.0.0/24"]
  target_tags   = ["http-server"]

  allow {
    protocol = "tcp"
    ports    = ["80", "443", "8080"]
  }

  depends_on = [
    google_compute_firewall.fw_iap
  ]
}
# [END cloudloadbalancing_int_http_gce]

# [START lb_route_extension]
resource "google_network_services_lb_route_extension" "{{$.PrimaryResourceId}}" {
  name                  = "{{index $.Vars "lb_route_extension_name"}}"
  description           = "my route extension"
  location              = "us-west1"
  load_balancing_scheme = "INTERNAL_MANAGED"
  forwarding_rules      = [google_compute_forwarding_rule.default.self_link]

  extension_chains {
    name = "chain1"

    match_condition {
        cel_expression = "request.path.startsWith('/extensions')"
    }

    extensions {
        name      = "ext11"
        authority = "ext11.com"
        service   = google_compute_region_backend_service.callouts_backend.self_link
        timeout   = "0.1s"
        fail_open = false

        forward_headers  = ["custom-header"]
    }
  }

  labels = {
    foo = "bar"
  }
}

# test instance
resource "google_compute_instance" "vm_test" {
  name         = "{{index $.Vars "vm_test_name"}}"
  zone         = "us-west1-b"
  machine_type = "e2-small"

  network_interface {
    network    = google_compute_network.ilb_network.id
    subnetwork = google_compute_subnetwork.ilb_subnet.id
  }

  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-11"
    }
  }
}

# Route Extension Backend Instance
resource "google_compute_instance" "callouts_instance" {
  name         = "{{index $.Vars "callouts_instance_name"}}"
  zone         = "us-west1-a"
  machine_type = "e2-small"

  labels = {
    "container-vm" = "cos-stable-109-17800-147-54"
  }

  tags = ["allow-ssh","load-balanced-backend"]

  network_interface {
    network    = google_compute_network.ilb_network.id
    subnetwork = google_compute_subnetwork.ilb_subnet.id

    access_config {
      # add external ip to fetch packages
    }
  }

  boot_disk {
    auto_delete  = true

    initialize_params {
      type  = "pd-standard"
      size  = 10
      image = "https://www.googleapis.com/compute/v1/projects/cos-cloud/global/images/cos-stable-109-17800-147-54"
    }
  }

  # Initialize an Envoy's Ext Proc gRPC API based on a docker container
  metadata = {
    startup-script = <<-EOF1
      #! /bin/bash
      apt-get update
      apt-get install apache2 -y
      a2ensite default-ssl
      a2enmod ssl
      echo "Page served from second backend service" | tee /var/www/html/index.html
      systemctl restart apache2'
    EOF1
  }

  lifecycle {
    create_before_destroy = true
  }

  deletion_protection = false

  depends_on = [
    google_compute_instance.vm_test
  ]
}

// callouts instance group
resource "google_compute_instance_group" "callouts_instance_group" {
  name        = "{{index $.Vars "callouts_instance_group"}}"
  description = "Terraform test instance group"
  zone        = "us-west1-a"

  instances = [
    google_compute_instance.callouts_instance.id,
  ]

  named_port {
    name = "http"
    port = "80"
  }

  named_port {
    name = "grpc"
    port = "443"
  }
}

# callout health check
resource "google_compute_region_health_check" "callouts_health_check" {
  name     = "{{index $.Vars "callouts_hc_name"}}"
  region   = "us-west1"

  http_health_check {
    port = 80
  }

  depends_on = [
    google_compute_region_health_check.default
  ]
}

# callout backend service
resource "google_compute_region_backend_service" "callouts_backend" {
  name                  = "{{index $.Vars "callouts_backend_name"}}"
  region                = "us-west1"
  protocol              = "HTTP2"
  load_balancing_scheme = "INTERNAL_MANAGED"
  timeout_sec           = 10
  port_name             = "grpc"
  health_checks         = [google_compute_region_health_check.callouts_health_check.id]

  backend {
    group           = google_compute_instance_group.callouts_instance_group.id
    balancing_mode  = "UTILIZATION"
    capacity_scaler = 1.0
  }

  depends_on = [
    google_compute_region_backend_service.default
  ]
}
# [END lb_route_extension]
