data "google_client_config" "current" {}

resource "google_compute_network" "apigee_network" {
  name = "apigee-network"
}

resource "google_compute_global_address" "apigee_range" {
  name          = "apigee-range"
  purpose       = "VPC_PEERING"
  address_type  = "INTERNAL"
  prefix_length = 16
  network       = google_compute_network.apigee_network.id
}

resource "google_service_networking_connection" "apigee_vpc_connection" {
  network                 = google_compute_network.apigee_network.id
  service                 = "servicenetworking.googleapis.com"
  reserved_peering_ranges = [google_compute_global_address.apigee_range.name]
}

resource "google_apigee_organization" "apigee_org" {
  analytics_region   = "us-central1"
  project_id         = data.google_client_config.current.project
  authorized_network = google_compute_network.apigee_network.id
  depends_on         = [google_service_networking_connection.apigee_vpc_connection]
}

resource "google_apigee_environment" "apigee_environment" {
  org_id       = google_apigee_organization.apigee_org.id
  name         = "tf-test-env"
  description  = "Apigee Environment"
  display_name = "Apigee Environment"
}

resource "google_apigee_instance" "apigee_instance" {
  name               = "instance"
  location           = "us-central1"
  org_id             = google_apigee_organization.apigee_org.id
  peering_cidr_range = "SLASH_22"
}

resource "google_apigee_api" "apigee_api" {
  name           = "tf-test-apigee-api"
  org_id         = google_apigee_organization.apigee_org.id
  config_bundle  = "./test-fixtures/apigee_api_bundle.zip"
}

resource "google_apigee_api_deployment" "apigee_api_deployment" {
  env_id   = google_apigee_environment.apigee_environment.id
  revision = google_apigee_api.apigee_api.revision[length(google_apigee_api.apigee_api.revision)-1]
  api_name = google_apigee_api.apigee_api.name
  depends_on = [
    google_apigee_instance.apigee_instance
  ]
}