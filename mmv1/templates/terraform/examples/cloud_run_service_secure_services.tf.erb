# [START cloud_run_secure_services_backend]
resource "google_cloud_run_service" "renderer" {
  name     = "<%= ctx[:vars]['renderer'] %>"
  location = "us-central1"
  template {
    spec {
      containers {
        # Replace with the URL of your Secure Services > Renderer image.
        #   gcr.io/<PROJECT_ID>/renderer
        image = "gcr.io/cloudrun/hello"
      }
      service_account_name = google_service_account.renderer.email
    }
  }
  traffic {
    percent         = 100
    latest_revision = true
  }
}
# [END cloud_run_secure_service_backend]

# [START cloud_run_secure_services_frontend]
resource "google_cloud_run_service" "editor" {
  name     = "<%= ctx[:vars]['editor'] %>"
  location = "us-central1"
  template {
    spec {
      containers {
        # Replace with the URL of your Secure Services > Editor image.
        #   gcr.io/<PROJECT_ID>/editor
        image = "gcr.io/cloudrun/hello"
        env {
          name  = "EDITOR_UPSTREAM_RENDER_URL"
          value = google_cloud_run_service.renderer.status[0].url
        }
      }
      service_account_name = google_service_account.editor.email
    }
  }
  traffic {
    percent         = 100
    latest_revision = true
  }
}
# [END cloud_run_secure_services_frontend]

# [START cloud_run_secure_services_backend_identity]
resource "google_service_account" "renderer" {
  account_id   = "renderer-identity"
  display_name = "Service identity of the Renderer (Backend) service."
}
# [END cloud_run_secure_services_backend_identity]

# [START cloud_run_secure_services_frontend_identity]
resource "google_service_account" "editor" {
  account_id   = "editor-identity"
  display_name = "Service identity of the Editor (Frontend) service."
}
# [END cloud_run_secure_services_frontend_identity]

# [START cloud_run_secure_services_backend_invoker_access]
resource "google_cloud_run_service_iam_member" "editor_invokes_renderer" {
  location = google_cloud_run_service.renderer.location
  service  = google_cloud_run_service.renderer.name
  role     = "roles/run.invoker"
  member   = "serviceAccount:${google_service_account.editor.email}"
}
# [END cloud_run_secure_services_backend_invoker_access]

# [START cloud_run_secure_services_frontend_access]
data "google_iam_policy" "noauth" {
  binding {
    role = "roles/run.invoker"
    members = [
      "allUsers",
    ]
  }
}

resource "google_cloud_run_service_iam_policy" "noauth" {
  location = google_cloud_run_service.editor.location
  project  = google_cloud_run_service.editor.project
  service  = google_cloud_run_service.editor.name

  policy_data = data.google_iam_policy.noauth.policy_data
}
# [END cloud_run_secure_services_frontend_access]

output "backend_url" {
  value = google_cloud_run_service.renderer.status[0].url
}

output "frontend_url" {
  value = google_cloud_run_service.editor.status[0].url
}

